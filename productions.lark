PROGRAM : DECL_LIBRARIES "class" ID "begin" DECL_VARIABLES STATEMENTS "end"

DECL_LIBRARIES : [ "import" ID_LIBRARY ";" DECL_LIBRARIES ]

DECL_VARIABLES : [ "def" VARIABLES "as" TYPE ";" DECL_VARIABLES ]
VARIABLES : ID [ "," VARIABLES ]
TYPE : "integer" | "float" | "char" | "string" | "boolean"

STATEMENTS : [ STATEMENT ";" STATEMENTS ]
STATEMENT : ASSIGNMENT | IF | WHILE | FOR | READ | WRITE | ENTER

ASSIGNMENT : ID "=" EXPRESSION_0

EXPRESSION_0 : EXPRESSION_1 [ "||" EXPRESSION_0 ]
EXPRESSION_1 : EXPRESSION_2 [ "&&" EXPRESSION_1]
EXPRESSION_2 : [ "!" ] EXPRESSION_3

EXPRESSION_3 : EXPRESSION_4 [ RELATIONAL_OPERATOR EXPRESSION_4 ]
RELATIONAL_OPERATOR : "==" | "!=" | "<" | "<=" | ">" | ">="

EXPRESSION_4 : ADDEND [ ( "+" | "-" ) EXPRESSION_4 ]

ADDEND : FACTOR [ ( "*" | "/" | "%" ) ADDEND ]

FACTOR : ID | CONST_INTEGER | CONST_REAL | CONST_REAL_SCI | CONST_CHAR | CONST_STRING | "(" EXPRESSION_0 ")"

IF : "if" "(" EXPRESSION_0 ")" STATEMENTS ELSE "endif"
ELSE : [ "else" STATEMENTS ]

WHILE : "while" "(" EXPRESSION_0 ")" STATEMENTS "endwhile"

FOR : "for" "(" ASSIGNMENT ":" EXPRESSION_0 ")" STATEMENTS "endfor"

EXPRESSIONS : EXPRESSION_0 [ "," EXPRESSIONS ]

READ : "read" "(" EXPRESSIONS ")"
WRITE : "write" "(" EXPRESSIONS ")"

ENTER : "\n"